buildscript {
    repositories {
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'org.spongepowered.plugin' version '0.9.0'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
}

allprojects {
    apply plugin: 'net.minecraftforge.gradle.forge'
    apply plugin: 'org.spongepowered.plugin'
    apply plugin: 'java'

    dependencies {
        compileOnly 'org.spongepowered:spongeapi:7.3.0-SNAPSHOT'
        compileOnly 'com.github.Eufranio:PixelBuilt-Quests:3.3'
    }

    repositories {
        maven { url 'https://jitpack.io' }
    }

    minecraft {
        // TODO: Configure Forge build here
        forgeVersion = '1.12.2-14.23.5.2847'
        // TODO: Replace with your mappings version, e.g. stable_39 or snapshot_20180814
        mappings = 'stable_39'
    }
}

dependencies {
    shadow project(':reforged')
    shadow project(':generations')
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier = null

    dependsOn reobfJar
    dependsOn project(':generations').tasks.reobfJar
    dependsOn project(':reforged').tasks.reobfJar

    from sourceSets.main.output
}

build.dependsOn shadowJar

group = pluginGroup
version = pluginVersion

sponge.plugin.id = pluginId
